import os
import sys
import math
from snakemake.utils import min_version

min_version("7.20")

MERYL = "/mmfs1/gscratch/stergachislab/mvollger/projects/k-mer-variant-phasing/bin/meryl-1.4/bin/"
HIPHASE = "/mmfs1/gscratch/stergachislab/mvollger/projects/k-mer-variant-phasing/bin/"
shell.prefix(f"export PATH={MERYL}:{HIPHASE}:$PATH; ")


CONDA = "../envs/env.yml"
# required
HIFI_BAM = config.pop("hifi_bam")
SAMPLE = config.pop("sample")
REFERENCE = config.pop("reference")
# optional
PAT_DATA = config.get("paternal", False)
MAT_DATA = config.get("maternal", False)
VCF = config.get("vcf", False)
K_MER_THREADS = config.get("k_mer_threads", 40)
K_MER_SIZE = config.get("k_mer_size", 31)
BIN_VERSION = config.get("BIN_VERSION", "1.5.0")
DEEPVARIANT = config.get("deepvariant", False)
ALIGN = config.get("align", False)
CLUSTER_PARTITION = config.get("cluster_partition", "compute")
# inferted
NO_PARENTAL = PAT_DATA is False or MAT_DATA is False

wildcard_constraints:
    sm="|".join([SAMPLE]),
    hap="pat|mat",
    individual="mat|pat|pro",
    scatteritem="\d+-of-\d+",


localrules:
    deepvariant,
    deepvariant_merge,


include: "rules/common.smk"
include: "rules/align.smk"
include: "rules/vcf.smk"
include: "rules/kmer.smk"
include: "rules/hiphase.smk"
include: "rules/merge.smk"


if DEEPVARIANT:

    rule all:
        input:
            rules.deepvariant.input,

elif NO_PARENTAL:

    rule all:
        input:
            expand(rules.hiphase.output, sm=SAMPLE),

else:

    rule all:
        input:
            expand(rules.kmer_read_list.output, sm=SAMPLE),
            expand(rules.merge_kmer_and_variant_phasing.output, sm=SAMPLE),
            expand(rules.haplotaged_bam.output, sm=SAMPLE),
            expand(rules.haplotaged_bai.output, sm=SAMPLE),
